(ns kotoba.lang.json-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.lang.json :as json]))

(deftest encode-scalars
  (is (= "null" (json/encode nil)))
  (is (= "true" (json/encode true)))
  (is (= "false" (json/encode false)))
  (is (= "5" (json/encode 5)))
  (is (= "5" (json/encode 5.0)))            ; integer-valued
  (is (= "1.5" (json/encode 1.5))))

(deftest encode-strings-escape
  (is (= "\"hello\"" (json/encode "hello")))
  (is (= "\"a\\nb\"" (json/encode "a\nb")))
  (is (= "\"a\\tb\"" (json/encode "a\tb")))
  (is (= "\"a\\\"b\"" (json/encode "a\"b")))
  (is (= "\"a\\\\b\"" (json/encode "a\\b"))))

(deftest encode-collections
  (is (= "[1,2,3]" (json/encode [1 2 3])))
  ;; deterministic key order: shorter first, then bytewise
  (is (= "{\"a\":1,\"bb\":2}" (json/encode {:bb 2 :a 1})))
  ;; keyword keys/values become strings
  (is (= "{\"k\":1}" (json/encode {:k 1})))
  (is (= "\"v\"" (json/encode :v))))

(deftest encode-nested
  (is (= "{\"a\":[1,{\"b\":2}]}" (json/encode {:a [1 {:b 2}]}))))

(deftest decode-scalars
  (is (= nil   (json/decode "null")))
  (is (= true  (json/decode "true")))
  (is (= false (json/decode "false")))
  (is (= 5     (json/decode "5")))
  (is (= 1.5   (json/decode "1.5")))
  (is (= -3    (json/decode "-3"))))

(deftest decode-strings
  (is (= "hello" (json/decode "\"hello\"")))
  (is (= "a\nb"  (json/decode "\"a\\nb\"")))
  (is (= "a\"b"  (json/decode "\"a\\\"b\"")))
  (is (= "a\\b"  (json/decode "\"a\\\\b\"")))
  (is (= "あ"    (json/decode "\"\\u3042\""))))

(deftest decode-collections
  (is (= {"a" 1 "b" [2 3]} (json/decode "{\"a\":1,\"b\":[2,3]}")))
  (is (= [1 2 3] (json/decode "[1,2,3]"))))

(deftest decode-whitespace
  (is (= {"a" 1} (json/decode "  {  \"a\" : 1  }  "))))

(deftest roundtrip
  (let [d {"name" "ada" "age" 36 "tags" ["a" "b"]}]
    (is (= d (json/decode (json/encode d))))))

(deftest decode-errors
  (is (thrown? clojure.lang.ExceptionInfo (json/decode "{")))
  (is (thrown? clojure.lang.ExceptionInfo (json/decode "[1,2,")))
  (is (thrown? clojure.lang.ExceptionInfo (json/decode "tru")))
  (is (thrown? clojure.lang.ExceptionInfo (json/decode "1 2")))) ; trailing
